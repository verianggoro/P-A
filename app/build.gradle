apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 1
ext.minimumSdkVersion = 16
ext.versionClassifier = null
ext.isSnapshot = false


private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 1000 + ext.versionMajor * 100 + ext.versionMinor * 10 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.pembelajar.phisicalalert"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
    }

    dataBinding{
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def appPrefixName = "Phisical_Alert"
            def date = new Date()
            def formattedDate = date.format('yyyy-MM-dd')
            def type = variant.buildType.name
            def versionName = "v" + defaultConfig.versionName;
            outputFileName = appPrefixName + "-" + type + "-" + versionName + "(" +
                    defaultConfig
                            .versionCode + ")-" + formattedDate + ".apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.2.0-alpha03'
    kapt 'com.android.databinding:compiler:3.1.4'
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
